prototype(Psmb.NodeTypes:ImageUri) < prototype(Neos.Fusion:Case) {
	image {
		condition = ${asset}
		renderer = Neos.Neos:ImageUri {
			asset = ${asset}
			width = ${width}
			height = ${height}
			maximumWidth = ${maximumWidth}
			maximumHeight = ${maximumHeight}
			allowCropping = ${allowCropping}
			allowUpscaling = ${allowUpscaling}
		}
	}
	dummyImage {
		condition = ${site.context.inBackend}
		renderer = Neos.Fusion:ResourceUri {
			path = 'resource://Neos.Neos/Public/Images/dummy-image.svg'
		}
	}
}



prototype(Psmb.NodeTypes:Text) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Text'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Headline) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Headline'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Lead) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Lead'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Blockquote) < prototype(Neos.Fusion:Tag) {
	tagName = 'blockquote'
	attributes.class = 'Blockquote'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Section) < prototype(Neos.Fusion:Tag) {
	tagName = 'section'
	attributes.class = 'Section'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Aside) < prototype(Neos.Fusion:Tag) {
	tagName = 'aside'
	attributes.class = 'Aside'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Small) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Small'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Epigraph) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Epigraph'
	content = Neos.Fusion:Tag {
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.convertUris = Neos.Neos:ConvertUris
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Image) < prototype(Neos.Fusion:Tag) {
	tagName = 'figure'
	content = Neos.Fusion:Array {
		editable = Neos.Neos:ContentElementEditable {
			property = 'image'
		}
		image = Neos.Fusion:Tag {
			tagName = 'img'
			attributes.alt = ${q(node).property('alt') || q(node).property('caption')}
			attributes.src = Psmb.NodeTypes:ImageUri {
				@context.asset = ${q(node).property('image')}
				@context.maximumWidth = 900
			}
		}
		caption = Neos.Fusion:Tag {
			tagName = 'figcaption'
			content = ${q(node).property('caption')}
			@if.captionIsSet = ${q(node).property('caption') ? true : false}
			@process.convertUris = Neos.Neos:ConvertUris
		}
	}
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
prototype(Psmb.NodeTypes:Asset) < prototype(Neos.Fusion:Tag) {
	tagName = 'a'
	attributes.href = ${'asset://' + node.properties.asset.identifier}
	attributes.href.@process.convertUris = Neos.Neos:ConvertUris
	content = ${q(node).property('title')}
	@process.wrap = ${'<div>' + value + '</div>'}
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}

prototype(Psmb.NodeTypes:Button) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Button button button--alt'
	content = Neos.Fusion:Tag {
		tagName = 'a'
		attributes.href = ${node.properties.target}
		attributes.href.@process.convertUris = Neos.Neos:ConvertUris {
			forceConversion = true
		}
		content = ${q(node).property('text')}
		@process.contentElementEditable = Neos.Neos:ContentElementEditable {
			property = 'text'
		}
	}
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}

prototype(Psmb.NodeTypes:ContainerOverImage) < prototype(Neos.Fusion:Tag) {
	@context.imageUri = Neos.Neos:ImageUri {
		asset = ${q(node).property('image')}
		maximumWidth = 1600
	}
	attributes.class = 'ContainerOverImage'
	attributes.style = ${'background-image: url(' + imageUri + ')'}
	content = Neos.Neos:ContentCollectionRenderer {
		@process.tmpl = ${'<div class="ContainerOverImage-inner">' + value + '</div>'}
		@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
	}
}

prototype(Psmb.NodeTypes:Container) < prototype(Neos.Fusion:Tag) {
	attributes.class = ${'Container ' + node.properties.class}
	attributes.id = ${node.properties.navTitle}
	content = Neos.Neos:ContentCollectionRenderer {
		@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
	}
}

prototype(Psmb.NodeTypes:Html) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Html'
	content = ${node.properties.source}
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}

prototype(Psmb.NodeTypes:Images) < prototype(Neos.Fusion:Tag) {
	attributes.class = 'Images'
	content = Neos.Neos:ContentCollectionRenderer {
		@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
	}
}

prototype(Psmb.NodeTypes:Accordion) < prototype(Neos.Neos:ContentComponent) {
	content = Neos.Fusion:Collection {
		collection = ${q(node).children('[instanceof Psmb.NodeTypes:AccordionItem]').get()}
		itemName = 'node'
		itemRenderer = Neos.Neos:ContentComponent {
			content = Neos.Neos:ContentCollectionRenderer {
				@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
			}
			renderer = afx`
				<div class="js-AccordionItem  backgroundColor-white" style="margin-bottom: 2px">
					<div href="#" class="js-AccordionItem-header AccordionItem-header padding-double margin-none beta" style="display: flex; justify-content: space-between">
						<div><Neos.Neos:Editable property="title" /></div>
						<div>
							<div class="AccordionItem-cross">
								<svg xmlns="http://www.w3.org/2000/svg" width="16" height="16" viewBox="0 0 612 612"><path d="M612 36.004L576.521.603 306 270.608 35.478.603 0 36.004l270.522 270.007L0 575.997l35.478 35.4L306 341.411l270.521 269.986 35.479-35.4-270.541-269.986z"/></svg>
							</div>
						</div>
					</div>
					<div style="display: none" class="AccordionItem-content padding-double">{props.content}</div>
				</div>
			`
		}
	}
	style = ${'<style>
		.AccordionItem-header:hover {
			cursor: pointer;
		}
		.isActive .AccordionItem-content{
			display: block !important;
		}
		.AccordionItem-cross {
			fill: #666;
			transition: transform 0.2s ease-in;
			transform: rotate(45deg);
		}
		.isActive .AccordionItem-cross {
			transform: rotate(0deg);
		}
	</style>'}
	renderer = afx`
		{props.style}
		<div class="js-Accordion backgroundColor-gray">{props.content}</div>
	`
}
